#!/usr/bin/env perl

use strict;
use warnings;

use LWP::UserAgent;
use JSON::XS;

my %annotations;
open my $fh, "-|:encoding(utf8)", "./bin/cldr-annotations" or die $!;

while (<$fh>) {
  if (/cp="([^"]+)">([^<]*)</) {
    $annotations{$1} = [ split /\s+\|\s+/, $2 ];
  }
}

my $url = "http://unicode.org/emoji/charts/full-emoji-list.html";
my $ua  = LWP::UserAgent->new;

my @emoji;

my $res = $ua->get($url);

die $res->status unless $res->is_success;

my @lines = split "\n", $res->decoded_content;
my %current;

for (@lines) {
  if (m{<td class='code'>.+U\+([^<]+)}) {
    $current{code} = $1;
    next;
  }
  elsif (m{<td class='chars'>([^<]+)}) {
    $current{chars} = $1;
    next;
  }
  elsif (m{<td class='name'>([^<]+)}) {
    $current{name} = $1;

    if ( my $keywords = $annotations{ $current{chars} } ) {
      $current{annotations} = $keywords;
    }

    push @emoji, {%current};
    %current = ();
    next;
  }
}

print encode_json \@emoji;
